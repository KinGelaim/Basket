<!doctype html>
<html lang="ru">
	<head>
		<meta charset="utf-8" />
		<title>Lesson 13 - Пиксельное освещение и несколько программ</title>
	</head>
	<body onload='webGLStart()'>
		<canvas id="canvas" width="800" height="600">
			Your browser does not support HTML5
		</canvas>

		<br/>
		<input type="checkbox" id="lighting" checked /> Использовать освещение<br/>
		<input type="checkbox" id="per-fragment" checked /> Пофрагментное освещение (попиксильное)<br/>
		<input type="checkbox" id="textures" checked /> Использовать текстуру<br/>
		<br/>

		<h2>Точечный свет:</h2>

		<table style="border: 0; padding: 10px;">
			<tr>
				<td><b>Позиция:</b>
				<td>X: <input type="text" id="lightPositionX" value="0.0" />
				<td>Y: <input type="text" id="lightPositionY" value="0.0" />
				<td>Z: <input type="text" id="lightPositionZ" value="-5.0" />
			</tr>
			<tr>
				<td><b>Цвет:</b>
				<td>R: <input type="text" id="pointR" value="0.8" />
				<td>G: <input type="text" id="pointG" value="0.8" />
				<td>B: <input type="text" id="pointB" value="0.8" />
			</tr>
		</table>

		<h2>Окружающий свет:</h2>

		<table style="border: 0; padding: 10px;">
			<tr>
				<td><b>Цвет:</b>
				<td>R: <input type="text" id="ambientR" value="0.2" />
				<td>G: <input type="text" id="ambientG" value="0.2" />
				<td>B: <input type="text" id="ambientB" value="0.2" />
			</tr>
		</table>
		
		<br/>
		<script type="text/javascript" src="../glMatrix-0.9.5.min.js"></script>
		<script type="text/javascript" src="../webgl-utils.js"></script>
		
		<script id="per-vertex-lighting-fs" type="x-shader/x-fragment">
			precision mediump float;

			varying vec2 vTextureCoord;
			varying vec3 vLightWeighting;

			uniform bool uUseTextures;

			uniform sampler2D uSampler;

			void main(void) {
				vec4 fragmentColor;
				if (uUseTextures) {
					fragmentColor = texture2D(uSampler, vec2(vTextureCoord.s, vTextureCoord.t));
				} else {
					fragmentColor = vec4(1.0, 1.0, 1.0, 1.0);
				}
				gl_FragColor = vec4(fragmentColor.rgb * vLightWeighting, fragmentColor.a);
			}
		</script>

		<script id="per-vertex-lighting-vs" type="x-shader/x-vertex">
			attribute vec3 aVertexPosition;
			attribute vec3 aVertexNormal;
			attribute vec2 aTextureCoord;

			uniform mat4 uMVMatrix;
			uniform mat4 uPMatrix;
			uniform mat3 uNMatrix;

			uniform vec3 uAmbientColor;

			uniform vec3 uPointLightingLocation;
			uniform vec3 uPointLightingColor;

			uniform bool uUseLighting;

			varying vec2 vTextureCoord;
			varying vec3 vLightWeighting;

			void main(void) {
				vec4 mvPosition = uMVMatrix * vec4(aVertexPosition, 1.0);
				gl_Position = uPMatrix * mvPosition;
				vTextureCoord = aTextureCoord;

				if (!uUseLighting) {
					vLightWeighting = vec3(1.0, 1.0, 1.0);
				} else {
					vec3 lightDirection = normalize(uPointLightingLocation - mvPosition.xyz);

					vec3 transformedNormal = uNMatrix * aVertexNormal;
					float directionalLightWeighting = max(dot(transformedNormal, lightDirection), 0.0);
					vLightWeighting = uAmbientColor + uPointLightingColor * directionalLightWeighting;
				}
			}
		</script>

		<script id="per-fragment-lighting-fs" type="x-shader/x-fragment">
			precision mediump float;

			varying vec2 vTextureCoord;
			varying vec3 vTransformedNormal;
			varying vec4 vPosition;

			uniform bool uUseLighting;
			uniform bool uUseTextures;

			uniform vec3 uAmbientColor;

			uniform vec3 uPointLightingLocation;
			uniform vec3 uPointLightingColor;

			uniform sampler2D uSampler;


			void main(void) {
				vec3 lightWeighting;
				if (!uUseLighting) {
					lightWeighting = vec3(1.0, 1.0, 1.0);
				} else {
					vec3 lightDirection = normalize(uPointLightingLocation - vPosition.xyz);

					float directionalLightWeighting = max(dot(normalize(vTransformedNormal), lightDirection), 0.0);
					lightWeighting = uAmbientColor + uPointLightingColor * directionalLightWeighting;
				}

				vec4 fragmentColor;
				if (uUseTextures) {
					fragmentColor = texture2D(uSampler, vec2(vTextureCoord.s, vTextureCoord.t));
				} else {
					fragmentColor = vec4(1.0, 1.0, 1.0, 1.0);
				}
				gl_FragColor = vec4(fragmentColor.rgb * lightWeighting, fragmentColor.a);
			}
		</script>

		<script id="per-fragment-lighting-vs" type="x-shader/x-vertex">
			attribute vec3 aVertexPosition;
			attribute vec3 aVertexNormal;
			attribute vec2 aTextureCoord;

			uniform mat4 uMVMatrix;
			uniform mat4 uPMatrix;
			uniform mat3 uNMatrix;

			varying vec2 vTextureCoord;
			varying vec3 vTransformedNormal;
			varying vec4 vPosition;


			void main(void) {
				vPosition = uMVMatrix * vec4(aVertexPosition, 1.0);
				gl_Position = uPMatrix * vPosition;
				vTextureCoord = aTextureCoord;
				vTransformedNormal = uNMatrix * aVertexNormal;
			}
		</script>
		<script src="webgl.js"></script>
	</body>
</html>